#!/usr/bin/gawk -f
#
# Usage: generate_table.sh < ${board}.out
#
# Takes the file generated by collect.sh and generates an ASCII
# table that can be inserted into the README.md.
#

{
    labels[0] = "Baseline"
    labels[1] = "ButtonConfig"
    labels[2] = "Encoded4To2ButtonConfig"
    labels[3] = "Encoded8To3ButtonConfig"
    labels[4] = "EncodedButtonConfig"
    labels[5] = "LadderButtonConfig"

    feature = NR-1
    u[feature]["flash"] = $2
    u[feature]["ram"] = $4
}
END {
    base_flash = u[0]["flash"]
    base_ram = u[0]["ram"]
    for (i = 0; i < NR; i++) {
        flash = u[i]["flash"]
        ram = u[i]["ram"]
        u[i]["d_flash"] = flash - base_flash
        u[i]["d_ram"] = ram - base_ram
    }

    printf("+--------------------------------------------------------------+\n")
    printf("| functionality                   |    flash/ram |       delta |\n")
    printf("|---------------------------------+--------------+-------------|\n")
    printf("| %-31s | %6d/%5d | %5d/%5d |\n",
        labels[0], u[0]["flash"], u[0]["ram"], u[0]["d_flash"], u[0]["d_ram"])
    printf("|---------------------------------+--------------+-------------|\n")
    for (i = 1; i <= 5; i++) {
        printf("| %-31s | %6d/%5d | %5d/%5d |\n",
            labels[i], u[i]["flash"], u[i]["ram"], u[i]["d_flash"],
            u[i]["d_ram"])
    }
    printf("+--------------------------------------------------------------+\n")
}
